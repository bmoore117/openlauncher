import java.time.LocalDate
import java.time.LocalDateTime

plugins {
    id 'com.android.application'
}

android {
    compileSdk 32

    defaultConfig {
        // will generate monotonically increasing build numbers that always fit within int max val,
        // padded with zeroes to be greater than the last build before this system was adopted
        double minuteOfDay = Math.floor(LocalDateTime.now().toLocalTime().toSecondOfDay() / 60.0)
        int hourOfDay = (int) Math.floor(minuteOfDay / 60.0)
        String padding = hourOfDay < 10 ? "00" : "0"
        String version = LocalDate.now().toString().replaceAll("-", "").substring(2) + hourOfDay + padding
        applicationId "net.skywall.openlauncher"
        minSdk 27
        targetSdk 32
        versionCode Integer.parseInt(version)
        versionName version

        flavorDimensions "versionCode"
        multiDexEnabled false

        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
        buildConfigField "String[]", "DETECTED_ANDROID_LOCALES", "${findUsedAndroidLocales()}"
        buildConfigField "String", "BUILD_DATE", "\"${getBuildDate()}\""
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""
        resValue "string", "manifest_package_id", "net.skywall.openlauncher"

        resValue 'string', 'app_name', "SkyWall"
    }

    productFlavors {
        flavorDefault {}

        flavorGplay {
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }

        flavorAtest {
            buildConfigField "boolean", "IS_NIGHTLY_BUILD", "true"
            versionName = new Date().format('yyMMdd')
            versionCode = Integer.parseInt(versionName)
            applicationId "net.skywall.openlauncher.nightly"
            resValue 'string', 'app_name', "SkyWall Nightly"
        }

        flavorGsantner {
            applicationId "net.gsantner.openlauncher"
            resValue 'string', 'app_name', "OL GS"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE-LGPL-2.1.txt', 'META-INF/LICENSE-LGPL-3.txt', 'META-INF/LICENSE-W3C-TEST']
        }
    }

    lint {
        abortOnError false
        disable 'MissingTranslation', 'InvalidPackage', 'ObsoleteLintCustomCheck', 'DefaultLocale', 'UnusedAttribute', 'VectorRaster', 'InflateParams', 'IconLocation', 'UnusedResources', 'TypographyEllipsis'
    }
    namespace 'net.skywall.openlauncher'

    allprojects {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

dependencies {
    // Support libraries
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    // UI Libs
    implementation 'com.mikepenz:fastadapter:3.2.3@aar'
    implementation 'com.mikepenz:fastadapter-commons:3.0.0@aar'
    implementation 'com.mikepenz:fastadapter-extensions:3.0.0@aar'
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'io.github.dreierf:material-intro-screen:0.0.6'
    implementation 'cat.ereza:customactivityoncrash:2.2.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'in.championswimmer:SimpleFingerGestures_Android_Library:1.1'
    implementation 'com.nononsenseapps:filepicker:4.2.1'
    implementation 'org.slf4j:slf4j-android:1.7.21'

    implementation ('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true
    }

    implementation('com.github.turing-tech:MaterialScrollBar:13.3.0') {
        exclude group: 'com.android.support'
    }
}
